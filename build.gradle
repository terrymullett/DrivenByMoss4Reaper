plugins {
    id 'com.github.jk1.dependency-license-report' version '1.3'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java' 
    id 'application'
}

version = '5.00'

jar {
    manifest {
        attributes(
            'Implementation-Title': 'DrivenByMoss4Reaper',
            'Implementation-Version': version,
            'Implementation-Vendor': 'Juergen Mossgraber'
        )
    }

    doLast {
        switch (OperatingSystem.get()) {
            case OperatingSystem.WINDOWS:
                String destDir = 'C:\\Users\\' + System.properties['user.name'] + '\\AppData\\Roaming\\Reaper\\UserPlugins\\drivenbymoss-libs'
                ant.copy (file: archivePath.getAbsolutePath(), todir: destDir)
                break;
        }
    }
}

distTar.enabled = false
startScripts.enabled = false

applicationDistribution.from('build/reports/dependency-license') {
    into "license/dependency"
}

installDist {
    doLast {
        File pluginFolder = new File (destinationDir, 'plugin')
        new File (destinationDir, 'lib').renameTo(pluginFolder)
        File subdir = new File (pluginFolder, 'drivenbymoss-libs')
        subdir.mkdirs ()
        String sourceDir = pluginFolder.getAbsolutePath()
        String destDir = subdir.getAbsolutePath()
        
        ant.delete () {
            fileset(dir: sourceDir) {
                include(name: 'purejavahidapi-*-javadoc.jar')
                include(name: 'purejavahidapi-*-sources.jar')
                include(name: 'libusb4java-1.3.0-linux-aarch64.jar')
                include(name: 'libusb4java-1.3.0-linux-arm.jar')
                include(name: 'libusb4java-1.3.0-linux-x86.jar')
                include(name: 'libusb4java-1.3.0-win32-x86.jar')
            }
        }
        ant.move (todir: destDir) {
            fileset(dir: sourceDir) {
                include(name: '**.jar')
            }
        }

        // Windows
        String zipFileName = 'build/DrivenByMoss4Reaper-' + version + '-Windows.zip'
        String runtimeDir = new File ('runtime', 'windows').getAbsolutePath()
        ant.zip (destfile: zipFileName) {
            fileset(dir: destinationDir)
            fileset(dir: runtimeDir)
        }

        // Mac
        zipFileName = 'build/DrivenByMoss4Reaper-' + version + '-Macos.zip'
        runtimeDir = new File ('runtime', 'macos').getAbsolutePath()
        ant.zip (destfile: zipFileName) {
            fileset(dir: destinationDir)
            fileset(dir: runtimeDir)
        }

        // Linux
        zipFileName = 'build/DrivenByMoss4Reaper-' + version + '-Linux.zip'
        runtimeDir = new File ('runtime', 'linux').getAbsolutePath()
        ant.zip (destfile: zipFileName) {
            fileset(dir: destinationDir)
            fileset(dir: runtimeDir)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.xmlgraphics', name: 'batik-anim', version: '1.10'
    compile group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.10'
    compile group: 'org.usb4java', name: 'usb4java', version: '1.3.0'
    compile group: 'com.illposed.osc', name: 'javaosc-core', version: '0.4'
    compile group: 'uk.co.xfactory-librarians', name: 'coremidi4j', version: '1.1'
	compile group: 'com.nikhaldimann', name: 'inieditor', version: 'r6'
    compile group: 'net.java.dev.jna', name: 'jna', version: '5.2.0'
	compile fileTree(dir: 'libs', include: '*.jar')
}

enum OperatingSystem
{
    WINDOWS,
    LINUX,
    MAC,
    OTHER;

    private static OperatingSystem os = OTHER;

    static
    {
        String osName = System.getProperty ("os.name");
        if (osName != null)
        {
            osName = osName.toLowerCase (Locale.ENGLISH);
            if (osName.contains ("windows"))
                os = WINDOWS;
            else if (osName.contains ("linux"))
                os = LINUX;
            else if (osName.contains ("mac os x"))
                os = MAC;
        }
    }

    public static OperatingSystem get ()
    {
        return os;
    }
}
