plugins {
    id 'com.github.jk1.dependency-license-report' version '1.2'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java' 
    id 'application'
}

version = '3.02'

jar {
    manifest {
        attributes(
            'Implementation-Title': 'DrivenByMoss4Reaper',
            'Implementation-Version': version,
            'Implementation-Vendor': 'Juergen Mossgraber'
        )
    }

    doLast {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            String destDir = 'C:\\Users\\' + System.properties['user.name'] + '\\AppData\\Roaming\\Reaper\\UserPlugins\\drivenbymoss-libs'
            ant.copy (file: archivePath.getAbsolutePath(), todir: destDir)
        }
    }
}

distTar.enabled = false
startScripts.enabled = false

applicationDistribution.from('src/main/cpp') {
    into "lib"
}
applicationDistribution.from('build/reports/dependency-license') {
    into "license/dependency"
}

installDist {
    doLast {
        File subdir = new File (destinationDir, '/lib/drivenbymoss-libs')
        subdir.mkdirs ()
        String sourceDir = new File (destinationDir, '/lib').getAbsolutePath()
        String destDir = subdir.getAbsolutePath()
        
        ant.delete () {
            fileset(dir: sourceDir) {
                include(name: 'purejavahidapi-*-javadoc.jar')
                include(name: 'purejavahidapi-*-sources.jar')
                include(name: 'libusb4java-1.2.0-linux-arm.jar')
                include(name: 'libusb4java-1.2.0-linux-x86.jar')
                include(name: 'libusb4java-1.2.0-osx-x86.jar')
                include(name: 'libusb4java-1.2.0-windows-x86.jar')
            }
        }

        ant.move (todir: destDir) {
            fileset(dir: sourceDir) {
                include(name: '**.jar')
            }
        }
        String zipFileName = 'build/DrivenByMoss4Reaper-' + version + '.zip'
        ant.zip (destfile: zipFileName, basedir: destinationDir)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.xmlgraphics', name: 'batik-anim', version: '1.10'
    compile group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.10'
    compile group: 'org.usb4java', name: 'usb4java', version: '1.2.0'
    compile group: 'com.illposed.osc', name: 'javaosc-core', version: '0.4'
    compile group: 'uk.co.xfactory-librarians', name: 'coremidi4j', version: '1.1'
	compile group: 'com.nikhaldimann', name: 'inieditor', version: 'r6'
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.2'
	compile fileTree(dir: 'libs', include: '*.jar')
}
