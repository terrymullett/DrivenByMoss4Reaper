plugins {
    // The following plugin causes the Gradle 7 deprecation warnings!
    id 'com.github.jk1.dependency-license-report' version '1.14'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'java'
    id 'application'
    id 'eclipse'
}

sourceCompatibility = 15
targetCompatibility = 15

version = '10.8.0'

jar {
    manifest {
        attributes(
            'Implementation-Title': 'DrivenByMoss4Reaper',
            'Implementation-Version': archiveVersion,
            'Implementation-Vendor': 'Juergen Mossgraber'
        )
    }

    doLast {
        switch (OperatingSystem.get()) {
            case OperatingSystem.WINDOWS:
                String destDir = 'C:\\Users\\' + System.properties['user.name'] + '\\AppData\\Roaming\\Reaper\\UserPlugins\\drivenbymoss-libs'
                ant.copy (file: archivePath.getAbsolutePath(), todir: destDir)
                break;
            case OperatingSystem.MAC:
                String destDir = '/Users/' + System.properties['user.name'] + '/Library/Application Support/REAPER/UserPlugins/drivenbymoss-libs'
                ant.copy (file: archivePath.getAbsolutePath(), todir: destDir)
                break;
            case OperatingSystem.LINUX:
                String destDir = '/home/' + System.properties['user.name'] + '/.config/REAPER/UserPlugins/drivenbymoss-libs'
                ant.copy (file: archivePath.getAbsolutePath(), todir: destDir)
                break;
        }
    }
}

distTar.enabled = false
startScripts.enabled = false

applicationDistribution.from('build/reports/dependency-license') {
    into "docs/license/dependency"
}

installDist {
    doLast {
        // Collect and move JAR files
        File javaLibsDir = new File (destinationDir, 'drivenbymoss-libs')
        javaLibsDir.mkdirs ()
        File sourceDir = new File (destinationDir, '/lib')
        ant.delete () {
            fileset(dir: sourceDir) {
                include(name: 'purejavahidapi-*-javadoc.jar')
                include(name: 'purejavahidapi-*-sources.jar')
                include(name: 'libusb4java-1.3.0-linux-aarch64.jar')
                include(name: 'libusb4java-1.3.0-linux-arm.jar')
                include(name: 'libusb4java-1.3.0-linux-x86.jar')
                include(name: 'libusb4java-1.3.0-win32-x86.jar')
            }
        }
        ant.move (todir: javaLibsDir) {
            fileset(dir: sourceDir) {
                include(name: '**.jar')
            }
        }
        sourceDir.delete()

        // Copy resources
        File resourcesDir = new File (destinationDir, 'resources')
        resourcesDir.mkdirs ()
        ant.copy (todir: resourcesDir) {
            fileset(dir: new File ('resources')) {
                include(name: '**')
            }
        }

        // Windows
        ant.zip (destfile: 'build/DrivenByMoss4Reaper-' + version + '-Windows.zip') {
            fileset(dir: destinationDir)
            fileset(dir: new File ('runtime', 'windows'))
        }

        // Mac
        ant.zip (destfile: 'build/DrivenByMoss4Reaper-' + version + '-Macos.zip') {
            fileset(dir: destinationDir) {
                exclude(name: '**/libusb4java-1.3.0-darwin-x86-64.jar')
            }
            fileset(dir: new File ('runtime', 'macos'))
            fileset(dir: new File ('libs', 'macos-fix')) {
                include(name: '**/libusb4java-1.3.0-darwin-x86-64-FIXED.jar')
            }
        }

        // Linux
        ant.zip (destfile: 'build/DrivenByMoss4Reaper-' + version + '-Linux.zip') {
            fileset(dir: destinationDir)
            fileset(dir: new File ('runtime', 'linux'))
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.headerOutputDirectory = file("build/headers/java/main")
}

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.usb4java', name: 'usb4java', version: '1.3.0'
    implementation group: 'com.illposed.osc', name: 'javaosc-core', version: '0.7'
    implementation group: 'uk.co.xfactory-librarians', name: 'coremidi4j', version: '1.4'
    implementation group: 'com.nikhaldimann', name: 'inieditor', version: 'r6'
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.6.0'
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.6.0'
    implementation group: 'com.weblookandfeel', name: 'svg-salamander', version: '1.1.2.2'
    implementation fileTree(dir: 'libs', include: '*.jar')
}

// Necessary to remove xml-api dependency introduced by Batik from Eclipse (it is already part of Java 11)
eclipse.classpath.file {
    whenMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' && entry.moduleVersion?.group == 'xml-apis' && entry.moduleVersion?.name == 'xml-apis' }
    }
}

enum OperatingSystem
{
    WINDOWS,
    LINUX,
    MAC,
    OTHER;

    private static OperatingSystem os = OTHER;

    static
    {
        String osName = System.getProperty ("os.name");
        if (osName != null)
        {
            osName = osName.toLowerCase (Locale.ENGLISH);
            if (osName.contains ("windows"))
                os = WINDOWS;
            else if (osName.contains ("linux"))
                os = LINUX;
            else if (osName.contains ("mac os x"))
                os = MAC;
        }
    }

    public static OperatingSystem get ()
    {
        return os;
    }
}
